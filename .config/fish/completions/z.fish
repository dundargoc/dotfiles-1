# Completions for `z`.
function __zoxide_z_complete
    set -l tokens (commandline -op)
    set -l curr_tokens (commandline -cop)

    if test (count $tokens) -le 2 -a (count $curr_tokens) -eq 1
        # If there are < 2 arguments, use `cd` completions.
        __fish_complete_directories "$tokens[2]" ''
    else if test (count $tokens) -eq (count $curr_tokens)
        # If the last argument is empty, use interactive selection.
        set -l query $tokens[2..-1]
        set -l result (zoxide query -i -- $query)
        and commandline -p "$tokens[1] "(string escape $result)
        commandline -f repaint
    end
end

complete -c z -f -a '(__zoxide_z_complete)'
